plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '1.3-SNAPSHOT'
description 'A generic implementation of EmotionML checks, in Java'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testCompileOnly 'junit:junit:4.13.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'emotionml.Main'
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                licenses {
                    license {
                        name = 'Creative Commons Zero v1.0 Universal'
                        url = 'https://creativecommons.org/publicdomain/zero/1.0/'
                    }
                }
                developers {
                    developer {
                        id = 'marc1s'
                        name = 'Marc Schr√∂der'
                    }
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/$System.env.GITHUB_REPOSITORY"
            credentials {
                username = System.env.GITHUB_ACTOR
                password = System.env.GITHUB_TOKEN
            }
        }
    }
}
