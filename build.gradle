plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'signing'
}

group 'de.dfki.mary'
version '1.3-SNAPSHOT'
description 'A generic implementation of EmotionML checks, in Java'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testCompileOnly libs.junit
    testRuntimeOnly libs.junit.vintage.engine
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'emotionml.Main'
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                def projectUrl = 'https://github.com/marytts/emotionml-checker-java'
                url = projectUrl
                licenses {
                    license {
                        name = 'Creative Commons Zero v1.0 Universal'
                        url = 'https://creativecommons.org/publicdomain/zero/1.0/'
                    }
                }
                developers {
                    developer {
                        id = 'marc1s'
                        name = 'Marc Schr√∂der'
                    }
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                }
                scm {
                    connection = "scm:git:${projectUrl}.git"
                    developerConnection = 'scm:git:ssh://github.com:marytts/emotionml-checker-java.git'
                    url = "$projectUrl/tree/master"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/$System.env.GITHUB_REPOSITORY"
            credentials {
                username = System.env.GITHUB_ACTOR
                password = System.env.GITHUB_TOKEN
            }
        }
        if (isReleaseVersion) {
            maven {
                name = 'OSSRH'
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials(PasswordCredentials)
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}
